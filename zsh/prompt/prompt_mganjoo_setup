# Custom prompt defnition.

+vi-git-status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2>/dev/null) ]]; then
    hook_com[unstaged]=' %F{red}●%f'
  fi
}

prompt_mganjoo_precmd() {
  setopt LOCAL_OPTIONS NO_XTRACE NO_KSH_ARRAYS

  # Obtain current working directory.
  local pwd="${PWD/#$HOME/~}"
  if [[ "$pwd" == (#m)[/~] ]]; then
    prompt_pwd="$MATCH"
    unset MATCH
  else
    prompt_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi

  # Get VCS information.
  vcs_info
}

prompt_mganjoo_setup() {
  setopt LOCAL_OPTIONS NO_XTRACE NO_KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load information for prompt.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling dynamic information before each command.
  add-zsh-hook precmd prompt_mganjoo_precmd

  # Set prompt style for keymap.
  zstyle ':prompt:indicator:keymap:insert' format '%B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prompt:indicator:keymap:command' format '❯❯❯'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git hg
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr ' %F{green}●%f'
  zstyle ':vcs_info:*' unstagedstr ' %F{yellow}●%f'
  zstyle ':vcs_info:*' formats ' on %F{green}%b%f%c%u'
  zstyle ':vcs_info:*' actionformats ' on %F{green}%b%f%c%u (%F{cyan}%a%f)'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  PROMPT='%F{yellow}%m%f${vcs_info_msg_0_}
%F{cyan}${prompt_pwd}%f ${editor_info[keymap]} '
  RPROMPT=''
  SPROMPT='correct %F{red}%R%f to %F{green}%r%f [nyae]? '

}

prompt_mganjoo_setup "$@"
